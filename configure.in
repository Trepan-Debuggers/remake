dnl Process this file with autoconf to produce a configure script.
AC_INIT(vpath.c)		dnl A distinctive file to look for in srcdir.
AC_CONFIG_HEADER(config.h)

# We want these before the checks, so the checks can modify their values.
test -z "$CFLAGS" && CFLAGS=-g AC_SUBST(CFLAGS)
test -z "$LDFLAGS" && LDFLAGS=-g AC_SUBST(LDFLAGS)

AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PROG_CPP			dnl Later checks need this.
AC_AIX
AC_ISC_POSIX
AC_MINIX
AC_STDC_HEADERS
AC_MEMORY_H
AC_UNISTD_H
AC_DIR_HEADER
AC_UID_T			dnl Also does gid_t.
AC_PID_T
AC_RETSIGTYPE
AC_HAVE_HEADERS(limits.h sys/param.h fcntl.h)
AC_MINUS_C_MINUS_O
AC_CONST			dnl getopt needs this.
AC_USG

AC_SUBST(LIBOBJS)

AC_HAVE_FUNCS(getdtablesize sys_siglist _sys_siglist \
	      dup2 getcwd sigsetmask getgroups setlinebuf \
	      setreuid setregid)
AC_ALLOCA
AC_VFORK
AC_SETVBUF_REVERSED
AC_GETLOADAVG
AC_STRCOLL

dnl Check out the wait reality.
AC_HAVE_HEADERS(sys/wait.h) AC_HAVE_FUNCS(waitpid wait3)
AC_COMPILE_CHECK(union wait, [#include <sys/types.h>
#include <sys/wait.h>],
	 	 [union wait status; int pid; pid = wait (&status);
#ifdef WEXITSTATUS
/* Some POSIXoid systems have both the new-style macros and the old
   union wait type, and they do not work together.  If union wait
   conflicts with WEXITSTATUS et al, we don't want to use it at all.  */
if (WEXITSTATUS (status) != 0) pid = -1;
#endif
],
		 AC_DEFINE(HAVE_UNION_WAIT))

AC_COMPILE_CHECK(sys_siglist declaration in signal.h,
		 [#include <signal.h>], [char *msg = *(sys_siglist + 1);],
		 AC_DEFINE(SYS_SIGLIST_DECLARED))

# The presence of the following is not meant to imply
# that make necessarily works on those systems.
AC_DYNIX_SEQ
AC_XENIX_DIR

AC_OUTPUT(Makefile build.sh glob/Makefile)

dnl Local Variables:
dnl comment-start: "dnl "
dnl comment-end: ""
dnl comment-start-skip: "\\bdnl\\b\\s *"
dnl compile-command: "make configure config.h.in"
dnl End:
